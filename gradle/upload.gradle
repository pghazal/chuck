apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

def siteUrl = 'https://github.com/pghazal/chuck'
def gitUrl = 'https://github.com/pghazal/chuck.git'

group = "com.pghazal.chuck"
version = '1.3.1'

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'

            name this.group
            description "Fork of Chuck Android HTTP interceptor ${this.version}"

            url siteUrl

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id POM_DEVELOPER_ID
                    name POM_DEVELOPER_NAME
                }
            }

            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

if (project.rootProject.file("local.properties").exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = properties.getProperty("bintrayUser")
        key = properties.getProperty("bintrayApiKey")

        configurations = ['archives']
        dryRun = false

        pkg {
            repo = 'Chuck'
            name = this.group
            websiteUrl = siteUrl
            vcsUrl = gitUrl
            publish = true
            publicDownloadNumbers = true
            licenses = ['Apache-2.0']
            version {
                name = this.version
                desc = "Fork of Chuck Android HTTP interceptor ${this.version}"
                released = new Date()
                vcsTag = this.version
            }
        }
    }
}